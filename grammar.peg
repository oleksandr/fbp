
package fbp

type Fbp Peg {
    BaseFbp
}

start <- line* _ !.

line <-
	_ "EXPORT=" [A-Za-z.0-9_]+ ":" [A-Z0-9_]+ _ LineTerminator?
  / _ "INPORT=" <[A-Za-z0-9_]+ "." [A-Z0-9_]+ ":" [A-Z0-9_]+> _ LineTerminator?   { p.createInport(buffer[begin:end]) }
  / _ "OUTPORT=" <[A-Za-z0-9_]+ "." [A-Z0-9_]+ ":" [A-Z0-9_]+> _ LineTerminator?    { p.createOutport(buffer[begin:end]) }
  / comment [\n\r]?
  / _ [\n\r]
  / _ connection _ LineTerminator?      

LineTerminator <- _ ","? comment? [\n\r]?

comment <- _ "#" anychar*

connection <-                 
  (                           
  	(                         
      bridge 
      _ "->" _                
      connection              
    )
    / bridge                  
  )                           

bridge <-
	(                           
    port _                                  { p.inPort = p.port }
    node _                    
    port                                    { p.outPort = p.port }
  )                                         { p.createMiddlet() }
  / iip                       
  / leftlet                                 { p.createLeftlet() }
  / rightlet                                { p.createRightlet() }

leftlet <-                    
  (
    node __                   
    port                      
  )                           

iip <- "'" <iipchar*> "'"                   { p.iip = buffer[begin:end] }       

rightlet <-                   
  (
    port _                    
    node                    
  )                           

node <-                       
  (
    <[a-zA-Z0-9_]+>                         { p.nodeProcessName = buffer[begin:end] }
    component?                
  )                                         { p.createNode() }

component <- 
  "("                         
    <[a-zA-Z/\-0-9_]*>                      { p.nodeComponentName = buffer[begin:end] }
    compMeta? 
  ")"                         

compMeta <- ":" <[a-zA-Z/=_,0-9]+>          { p.nodeMeta = buffer[begin:end] }  

port <- <[A-Z.0-9_]+> __                    { p.port = buffer[begin:end] }        

anychar <- [^\n\r]

iipchar <-
	[\\][']
  / [^']

_ <- [ \t]*

__ <- [ \t]+
